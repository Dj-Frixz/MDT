WARNING: Accepts only two numbers delimited by a " " as input!

States:
0 → (initial state) moves the reading/writing head (rwh) from any digit of the 1st number to the leftmost digit of the 2nd number
1 → (follows state <0>) moves the rwh to the rightmost digit of the 2nd number
sc/sc0 → moves the rwh from the rightmost digit of the 2nd number to the rightmost digit/char of the 1st number
diff → (difference) subtracts 1
sum → adds 1 on the rightmost digit found
conv → converts digits to characters of the alphabet (0,1,2,...,9 → z,a,b,...,i)
unc → converts characters to digits
stop → end-state

(0,0,0,0,>)
(0,1,0,1,>)
(0,2,0,2,>)
(0,3,0,3,>)
(0,4,0,4,>)
(0,5,0,5,>)
(0,6,0,6,>)
(0,7,0,7,>)
(0,8,0,8,>)
(0,9,0,9,>)
(0,-,1,-,>)
(0,a,0,a,>)
(0,b,0,b,>)
(0,c,0,c,>)
(0,d,0,d,>)
(0,e,0,e,>)
(0,f,0,f,>)
(0,g,0,g,>)
(0,h,0,h,>)
(0,i,0,i,>)
(0,z,0,z,>)
(1,0,1,0,>)
(1,1,1,1,>)
(1,2,1,2,>)
(1,3,1,3,>)
(1,4,1,4,>)
(1,5,1,5,>)
(1,6,1,6,>)
(1,7,1,7,>)
(1,8,1,8,>)
(1,9,1,9,>)
(1,-,diff,-,<)
(diff,0,sc0,-,<)
(diff,1,sc,0,<)
(diff,2,sc,1,<)
(diff,3,sc,2,<)
(diff,4,sc,3,<)
(diff,5,sc,4,<)
(diff,6,sc,5,<)
(diff,7,sc,6,<)
(diff,8,sc,7,<)
(diff,9,sc,8,<)
(diff,-,unc,-,<)
(sc,0,sc,0,<)
(sc,1,sc,1,<)
(sc,2,sc,2,<)
(sc,3,sc,3,<)
(sc,4,sc,4,<)
(sc,5,sc,5,<)
(sc,6,sc,6,<)
(sc,7,sc,7,<)
(sc,8,sc,8,<)
(sc,9,sc,9,<)
(sc,-,sum,-,<)
(sc0,0,sc0,0,<)
(sc0,1,sc0,1,<)
(sc0,2,sc0,2,<)
(sc0,3,sc0,3,<)
(sc0,4,sc0,4,<)
(sc0,5,sc0,5,<)
(sc0,6,sc0,6,<)
(sc0,7,sc0,7,<)
(sc0,8,sc0,8,<)
(sc0,9,sc0,9,<)
(sc0,-,conv,-,<)
(sum,0,0,1,>)
(sum,1,0,2,>)
(sum,2,0,3,>)
(sum,3,0,4,>)
(sum,4,0,5,>)
(sum,5,0,6,>)
(sum,6,0,7,>)
(sum,7,0,8,>)
(sum,8,0,9,>)
(sum,9,sum,0,<)
(sum,-,0,1,>)
(sum,a,sum,a,<)
(sum,b,sum,b,<)
(sum,c,sum,c,<)
(sum,d,sum,d,<)
(sum,e,sum,e,<)
(sum,f,sum,f,<)
(sum,g,sum,g,<)
(sum,h,sum,h,<)
(sum,i,sum,i,<)
(sum,z,sum,z,<)
(conv,0,0,z,>)
(conv,1,0,a,>)
(conv,2,0,b,>)
(conv,3,0,c,>)
(conv,4,0,d,>)
(conv,5,0,e,>)
(conv,6,0,f,>)
(conv,7,0,g,>)
(conv,8,0,h,>)
(conv,9,0,i,>)
(conv,a,conv,a,<)
(conv,b,conv,b,<)
(conv,c,conv,c,<)
(conv,d,conv,d,<)
(conv,e,conv,e,<)
(conv,f,conv,f,<)
(conv,g,conv,g,<)
(conv,h,conv,h,<)
(conv,i,conv,i,<)
(conv,z,conv,z,<)
(unc,a,unc,1,<)
(unc,b,unc,2,<)
(unc,c,unc,3,<)
(unc,d,unc,4,<)
(unc,e,unc,5,<)
(unc,f,unc,6,<)
(unc,g,unc,7,<)
(unc,h,unc,8,<)
(unc,i,unc,9,<)
(unc,z,unc,0,<)
(unc,-,stop,-,>)

The concept here is to simulate how humans calculate a sum,
so adding firstly the units, secondly the tens, than the hundreds and so on.
The complexity depends on how big are the digits of the number on the right.
For example "100+99" would take more time than "100+1111".
